/////////////
// PLUGINS //
/////////////

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id "com.jfrog.bintray" version "1.8.4"
    id "java-library"
    id "maven-publish"
    id "jacoco"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
    id "io.gitlab.arturbosch.detekt" version "1.5.1"
    id "com.github.ben-manes.versions" version "0.27.0"
}

//////////////////
// DEPENDENCIES //
//////////////////

repositories {
    jcenter()
}

ext {
    kodeinVersion = "6.5.2"
    slf4jVersion = "1.7.30"
}

dependencies {
    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // KODEIN
    implementation "org.kodein.di:kodein-di-generic-jvm:$kodeinVersion"

    // LOGGING
    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    // TESTING
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.0"
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"
}

/////////////
// UPDATES //
/////////////

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

/////////////
// COMPILE //
/////////////

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    dependsOn ktlintFormat
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

//////////
// TEST //
//////////

ktlint {
    version = "0.36.0"
    verbose = true
}

detekt {
    config = files("detekt.yml")
    buildUponDefaultConfig = true
}

test {
    useJUnitPlatform {
        excludeTags project.hasProperty("fast") ? "Slow" : "None"
    }
}

jacoco {
    toolVersion = "0.8.4"
}

if (!project.hasProperty("fast")) test.finalizedBy(jacocoTestReport)

/////////////
// PUBLISH //
/////////////

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = "javadoc"
}

publishing {
    publications {
        bintrayMaven(MavenPublication) {
            groupId = project.group
            artifactId = projectName
            version = project.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "Kodein Application"
                description = "Set of functions to create applications based on Kodein container"
                url = "https://github.com/Koriit/kodein-application"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/Koriit/kodein-application/blob/master/LICENSE"
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getProperty("bintray.user")
    key = System.getProperty("bintray.key")
    publications = ["bintrayMaven"]

    pkg {
        repo = "kotlin"
        name = "kodein-application"
        version {
            name = project.version
            released  = new Date()
        }
    }
}

///////////
// OTHER //
///////////

task getVersion {
    doLast {
        print project.version
    }
}

task fmt {
    dependsOn ktlintFormat
}
